# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.2
pkgrel=9999
pkgdesc="S-Lang is a powerful interpreted language"
arch=('aarch64' 'x86_64')
url="https://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(
  https://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2
  slang_utf8db.diff
)
sha1sums=('bbf7f2dcc14e7c7fca40868fd4b411a2bd9e2655'
          '275aee8ae9af50fb064e81ee5ed196996f019f5a')
groups=('modified')

build() {
  cd ${pkgname}-${pkgver}
  patch --forward --strip=1 --input="${srcdir}/slang_utf8db.diff"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH = "i686" ]] || make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
